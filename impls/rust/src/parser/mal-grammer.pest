values = _{ SOI ~ value* ~ EOI }
value = _{ (atom | list | vector | map) }

list = { "(" ~ value* ~ ")" }
vector = { "[" ~ value* ~ "]" }
map = { "{" ~ (atom ~ value)* ~ "}" }

atom = _{ (number | symbol | keyword | string | boolean | nil) }
number = @{ ("-" | "+")? ~ ASCII_DIGIT+ }
symbol = @{
    !ASCII_DIGIT
    ~
    (ASCII_ALPHA | atom_chars)
    ~
    (ASCII_ALPHANUMERIC | atom_chars)*
}
atom_chars = { ("-" | "=" | "+" | "*" | "/" | "|" | "<" | ">" | "!" | "_") }
keyword = @{ ":" ~ symbol }

string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ char* }
char = _{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

boolean = { "true" | "false" }
nil = { "nil" }

quote = @{ "'" ~ value }

WHITESPACE = _{ " " | "\t" | "," | NEWLINE }
COMMENT    = _{ ";" ~ (!NEWLINE ~ ANY)* }
